apply from: "${project.rootDir}/gradle/dependencies.gradle"

apply plugin: 'com.github.ben-manes.versions'

buildscript {
  repositories {
    google()
    mavenCentral()
    jcenter()
  }

  dependencies {
    classpath 'com.android.tools.build:gradle:4.1.2'

    classpath 'com.github.ben-manes:gradle-versions-plugin:0.36.0'
    classpath 'com.vanniktech:gradle-maven-publish-plugin:0.14.2'
  }
}

allprojects {
  repositories {
    google()
    jcenter()
  }
}

wrapper {
  gradleVersion = '6.8.2'
  //noinspection UnnecessaryQualifiedReference
  distributionType = Wrapper.DistributionType.ALL
}

def isNonStable = { String version ->
  def stableKeyword = ['RELEASE', 'FINAL', 'GA'].any { it -> version.toUpperCase().contains(it) }
  def regex = /^[0-9,.v-]+(-r)?$/
  return !stableKeyword && !(version ==~ regex)
}

tasks.named("dependencyUpdates").configure {
  // Example 1: reject all non stable versions
  rejectVersionIf {
    isNonStable(it.candidate.version)
  }
}
